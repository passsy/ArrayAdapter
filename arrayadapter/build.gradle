apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            // output coverage with ./gradlew clean build createDebugCoverageReport
            testCoverageEnabled gradle.startParameter.taskNames.any { name ->
                name.matches(".*AndroidTest.*") ||
                        name.matches(".*Check.*") ||
                        name.matches(".*create.*CoverageReport.*") ||
                        name.matches("tasks")
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    androidTestCompile "junit:junit:$junitVersion"
    androidTestCompile "org.assertj:assertj-core:$assertjVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "org.mockito:mockito-android:$mockitoVersion"
}

// Publish to bintray
// ./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=true
publish {
    userOrg = 'passsy'
    groupId = 'com.pascalwelsch.arrayadapter'
    publishVersion = VERSION_NAME
    artifactId = 'arrayadapter'
    uploadName = 'ArrayAdapter'
    description = 'the missing ArrayAdapter implementation for RecyclerView'
    website = 'https://github.com/passsy/ArrayAdapter'
}
